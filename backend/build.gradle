plugins {
    id 'java-library'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    // Required by lombok
    id 'net.ltgt.apt' version '0.21'
    id 'com.github.spotbugs' version '4.0.4'
//    Uncomment only to get license info : https://github.com/hierynomus/license-gradle-plugin/issues/161
//    id 'com.github.hierynomus.license-report' version '0.15.0'

//    https://github.com/ben-manes/gradle-versions-plugin
    id "com.github.ben-manes.versions" version "0.27.0"
}

allprojects {
    apply plugin: 'jacoco'

    group = 'com.kraken'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
        maven {
            url "https://www.license4j.com/maven/"
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    jacoco {
        toolVersion = '0.8.5'
    }
}

subprojects {
    repositories {
        maven {
            url "https://www.license4j.com/maven/"
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    buildscript {
        repositories {
            mavenCentral()
        }
    }

    ext {
        springBootVersion = '2.2.5.RELEASE'
        springReactorVersion = '3.3.3.RELEASE'
        springSecurityVersion = '5.3.0.RELEASE'
        lombokVersion = '1.18.12'
        guavaVersion = '28.2-jre'
        equalsVerifierVersion = '3.1.12'
        ztZipVersion = '1.14'
        okhttpVersion = '4.4.0'
        jacksonVersion = '2.10.2'
    }

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'idea'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'net.ltgt.apt'
    apply plugin: 'com.github.spotbugs'

    sourceCompatibility = 11

    dependencies {
        // Spring boot
        // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-webflux
        implementation group: 'org.springframework.boot', name: 'spring-boot-starter-webflux', version: springBootVersion

        // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-test
        testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: springBootVersion
        // https://mvnrepository.com/artifact/io.projectreactor/reactor-test
        testImplementation group: 'io.projectreactor', name: 'reactor-test', version: springReactorVersion
        // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-devtools
        testImplementation group: 'org.springframework.boot', name: 'spring-boot-devtools', version: springBootVersion

        // Lombok
        implementation group: 'org.projectlombok', name: 'lombok', version: lombokVersion
        testCompileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
        annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
        testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion

        // https://mvnrepository.com/artifact/com.google.guava/guava
        implementation group: 'com.google.guava', name: 'guava', version: guavaVersion
        // https://mvnrepository.com/artifact/com.google.guava/guava-testlib
        testImplementation group: 'com.google.guava', name: 'guava-testlib', version: guavaVersion

        // https://mvnrepository.com/artifact/nl.jqno.equalsverifier/equalsverifier
        testImplementation group: 'nl.jqno.equalsverifier', name: 'equalsverifier', version: equalsVerifierVersion
        // https://mvnrepository.com/artifact/com.google.code.findbugs/annotations
        implementation group: 'com.google.code.findbugs', name: 'annotations', version: '3.0.1'
    }

    dependencyManagement {
        imports { mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}") }
    }

    configurations {
        testArtifacts.extendsFrom testRuntime
    }
    
    task testJar(type: Jar) {
        archiveClassifier.set('test')
        from sourceSets.test.output
    }
    
    artifacts {
        testArtifacts testJar
    }

    jacocoTestReport {
        reports {
            html.enabled = true
            xml.enabled = true
            csv.enabled = false
        }
    }

    check.dependsOn jacocoTestReport


    spotbugsMain {
        excludeFilter = file("$rootProject.projectDir/spotbugs-exclude.xml")
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    spotbugsTest {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    check.dependsOn spotbugsMain
}

task jacocoRootReport(type: JacocoReport) {
    dependsOn = subprojects.test
    additionalSourceDirs.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.setFrom files(subprojects.sourceSets.main.output)
    executionData.setFrom project.fileTree(dir: '.', include: '**/build/jacoco/test.exec')
    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
    }
    onlyIf = {
        true
    }
    doFirst {
        getExecutionData().setFrom(files(executionData.findAll {
            it.exists()
        }))
    }
}

project(':').jar { onlyIf { false } }
project(':tests').jar { onlyIf { false } }
project(':applications').jar { onlyIf { false } }
project(':applications:ee').jar { onlyIf { false } }
project(':commons').jar { onlyIf { false } }
project(':commons:analysis').jar { onlyIf { false } }
project(':commons:analysis:container').jar { onlyIf { false } }
project(':commons:ee').jar { onlyIf { false } }
project(':commons:gatling').jar { onlyIf { false } }
project(':commons:parser').jar { onlyIf { false } }
project(':commons:parser:container').jar { onlyIf { false } }
project(':commons:runtime').jar { onlyIf { false } }
project(':commons:security').jar { onlyIf { false } }
project(':commons:sse').jar { onlyIf { false } }
project(':commons:storage').jar { onlyIf { false } }
project(':commons:template').jar { onlyIf { false } }
project(':commons:tools').jar { onlyIf { false } }
project(':commons:config').jar { onlyIf { false } }